using System;
using System.Collections.Generic;
using System.Linq;
using System.Text.RegularExpressions;

namespace Mal
{
	public class Reader
	{
		public this(s : string)
		{
			tokens = Tokenizer(s);
		}

		mutable index = 0;
		mutable tokens : array[string];

		private static Tokenizer(source : string) : array[string]
		{
			def rx = "[\\s,]*(~@|[\\[\\]{}()'`~^@]|\"(?:\\\\.|[^\\\\\"])*\"|;.*|[^\\s\\[\\]{}('\"`,;)]*)";
			def m = Regex(rx).Matches(source);

			// copy matches to array of strings
			def result = array(m.Count);
			for (mutable i = 0; i < m.Count; i++) 
			{
				result[i] = m[i].Value.Trim()
			}

			result
		}

		public Next() : string
		{
			def i = index;
			index++;
			tokens[i];
		}

		public Peek() : string
		{
			tokens[index];
		}

		public ReadAtom() : object
		{
			Next()
		}

		public ReadList() : List[object]
		{
			_ = Next(); // skip "("

			def result = List();
			def loop() : void
			{
				def token = Peek();
				match (token)
				{
					| ")" => _ = Next(); // skip ")"
					| _ => result.Add(ReadForm()); loop()
				}
			}

			loop();
			result
		}

		public ReadForm() : object
		{
			def token = Peek();
			match (token)
			{
				| "(" => ReadList()
				| _ => ReadAtom()
			}
		}

	}

	/* Tests for Reader:
	class Program
	{
		// tokenizer
		static _Main() : void
		{
			def src = "(def plus2(x) (+ x 2))";
			def r = Reader(src);
			def loop()
			{
				def token = r.Next();
				when (!string.IsNullOrWhiteSpace(token))
				{
					Console.WriteLine(token);
					loop();
				}
			}

			loop()
		}

		// form reader
		static Main() : void
		{
			def src = "(+ 1 2 3 (* 6 (- 7 2) 8) 4 5)"; //"(def test(x) (+ x (* 2 12) (test (- x 1))))";
			def r = Reader(src);
			def form = r.ReadForm();

			def print(indent : string, form : object)
			{
				match (form)
				{
					| lst is List[object] =>
					{
						foreach (l in lst)
						{
							print(indent + " ", l);
						}
					}
					| _ =>
					{
						Console.Write(indent);
						Console.WriteLine(form);
					}
				}
			}

			Console.WriteLine(src);
			print("", form)
		}
	}*/
}
